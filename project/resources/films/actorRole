SELECT		
		DISTINCT 
		(replace(str(?actor), 'http://www.wikidata.org/entity/', '') as ?authorId)
		(?actorLabel as ?authorName)
		(replace(str(?film), 'http://www.wikidata.org/entity/', '') as ?artWorkId)
		(?filmLabel as ?artWorkTitle)
		(?roleLabel as ?role)
		#?ActorRoleFilm
 WHERE								
{								
  		?film			wdt:P31			wd:Q11424 .
		?film			rdfs:label		?filmLabel .	
  		?film			wdt:P161		wd:Q43416 .
  		?film 			wdt:P161 		?actor .	
  		?actor 			rdfs:label 		?actorLabel .	
	OPTIONAL {							
  		?film 			p:P161			?statement .
  		?statement		ps:P161 		?actor .		
  		?statement		pq:P453			?role .	
  		?role			rdfs:label		?roleLabel .	
  		FILTER (lang(?roleLabel) = 'en') .  						
	}							
  		FILTER (lang(?actorLabel) = 'en') .						
  		FILTER (lang(?filmLabel) = 'en') .						
		BIND(if(bound(?roleLabel),
           		concat('(', replace(str(?actor), 'http://www.wikidata.org/entity/', ''), ')-[:ACTED_IN {roles:["', ?roleLabel, '"]}]->(', replace(str(?film), 'http://www.wikidata.org/entity/', ''), '),'),
           		concat('(', replace(str(?actor), 'http://www.wikidata.org/entity/', ''), ')-[:ACTED_IN]->(', replace(str(?film), 'http://www.wikidata.org/entity/', ''), '),'))
          AS ?ActorRoleFilm) .
}
ORDER BY ?filmLabel ?actorLabel